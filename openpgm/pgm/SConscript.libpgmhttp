# -*- mode: python -*-
# OpenPGM build script
# $Id$

Import('env')

e = env.Clone()
e.Append(CCFLAGS = '-DG_LOG_DOMAIN=\'"Pgm-Http"\'');

# add binary tree to include path to find embedded htdocs
e.Append(CPPPATH = '.');

e.MergeFlags(e['LIBSOUP_FLAGS']);

src = Split("""
		http.c
""")

htdocs = Split("""
		htdocs/404.html
		htdocs/base.css
		htdocs/robots.txt
		htdocs/xhtml10_strict.doctype
""")

pgmhttp = e.StaticLibrary('libpgmhttp', src);
pgmhttppic = e.StaticSharedLibrary('libpgmhttp-pic', src);

# embed htdocs into C headers
embed_htdoc = Builder(action = './htdocs/convert_to_macro.pl $SOURCE > $TARGET')
e.Append(BUILDERS = {'EmbedHtdoc' : embed_htdoc})

for htdoc in htdocs:
	embedded = htdoc + '.h'
	e.EmbedHtdoc(embedded, htdoc)

# end of file
