# -*- mode: python -*-
# OpenPGM build script
# $Id$

Import('env')

lib_src = Split("""
		pgm.c
		timer.c
		if.c
		gsi.c
		signal.c
		txwi.c
		rxwi.c
		transport.c
		rate_control.c
		async.c
""")

lib_env = env.Copy();

lib_obj = []
for src in lib_src:
	lib_obj += lib_env.SharedObject(src)

lib_env.StaticLibrary('libpgm', lib_src);
lib_env.StaticLibrary('libpgm-pic', lib_obj);


# GPL license, aka plus version
#

plus_src = Split("""
		reed_solomon.c
""")

plus32_src = Split("""
		checksum32.S
""")

plus64_src = Split("""
		csum-copy64.S
		csum-partial64.c
""")

plus_env = env.Copy();
plus_env['SHOBJSUFFIX'] = '.plus' + plus_env['SHOBJSUFFIX'];
plus_env['OBJSUFFIX'] = '.plus' + plus_env['OBJSUFFIX'];
plus_env.Append(CCFLAGS = '-DCONFIG_CKSUM_COPY');
plus_env.Append(CCFLAGS = '-DCONFIG_REED_SOLOMON');

plus_obj = []
for src in lib_src:
	plus_obj += plus_env.SharedObject(src)
for src in plus_src:
	plus_obj += plus_env.SharedObject(src)

if env['BUILD'] == 'thirtytwo':
	for src in plus32_src:
		plus_src += plus_env.StaticObject(src)
		plus_obj += plus_env.SharedObject(src)
if env['BUILD'] != 'thirtytwo':
	for src in plus64_src:
		plus_src += plus_env.StaticObject(src)
		plus_obj += plus_env.SharedObject(src)

plus_env.StaticLibrary('libpgmplus', lib_src + plus_src);
plus_env.StaticLibrary('libpgmplus-pic', plus_obj);

# end of file
