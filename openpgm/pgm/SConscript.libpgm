# -*- mode: python -*-
# OpenPGM build script
# $Id$

import os;
Import('env')

src = Split("""
		packet.c
		time.c
		if.c
		getifaddrs.c
		getnodeaddr.c
		indextoaddr.c
		inet_network.c
		md5.c
		gsi.c
		signal.c
		txwi.c
		rxwi.c
		transport.c
		source.c
		receiver.c
		pgm.c
		timer.c
		net.c
		rate_control.c
		async.c
		checksum.c
		reed_solomon.c
		galois_tables.c
""")

e = env.Clone();
e.Append(CCFLAGS = '-DG_LOG_DOMAIN=\'"Pgm"\'');
e.Append(CCFLAGS = '-DGETTEXT_PACKAGE=\'"pgm"\'');

# Galois tables
e.Command ('galois_tables.c', 'galois_generator.pl', "perl $SOURCE > $TARGET");

# Version stamping
e.Command ('version.c', 'version_generator.py', "python $SOURCE > $TARGET");
e.Depends ('version.c', src);
src += ['version.c'];

e.StaticLibrary('libpgm', src);
e.StaticSharedLibrary('libpgm-pic', src);

#-----------------------------------------------------------------------------
# unit testing

if env['WITH_CHECK'] == 'true':
	te = e.Clone();
	newCCFLAGS = [];
	for flag in te['CCFLAGS']:
		if ("-W" != flag[:2]) and ("-pedantic" != flag[:9]):
			newCCFLAGS.append(flag);
	te['CCFLAGS'] = newCCFLAGS;
	te.ParseConfig ('pkg-config --static --cflags --libs check');
	te.Program ('txwi_unittest.c');
	te.Program ('rxwi_unittest.c');
	te.Program ('pgm_unittest.c');
	te.Program (['gsi_unittest.c', e.Object('md5.c')]);
	te.Program (['if_unittest.c', e.Object('inet_network.c')]);
	te.Program (['transport_unittest.c', e.Object('indextoaddr.c'), e.Object('getnodeaddr.c'), e.Object('if.c'), e.Object('inet_network.c')]);

#-----------------------------------------------------------------------------
# GPL license, aka plus version

if env['WITH_PLUS'] == 'true':
	pe = env.Clone();
	pe.Append(CCFLAGS = '-DG_LOG_DOMAIN=\'"Pgm"\'');
	pe.Append(CCFLAGS = '-DGETTEXT_PACKAGE=\'"pgm"\'');

# add new suffix so objects don't interfere with LGPL license library
	pe['SHOBJSUFFIX'] = '.plus' + pe['SHOBJSUFFIX'];
	pe['OBJSUFFIX']   = '.plus' + pe['OBJSUFFIX'];

	uname_s = os.uname()[0];
	if uname_s == 'Linux':
		uname_m = os.uname()[4];
		if uname_m == 'x86_64':
			pe.Append(CCFLAGS = '-DCONFIG_CKSUM_COPY');
			src += ['csum-copy64.S', 'csum-partial64.c'];
		else:
			pe.Append(CCFLAGS = '-DCONFIG_CKSUM_COPY');
			src += ['checksum32.S'];
		pe.StaticLibrary('libpgmplus', src);
		pe.StaticSharedLibrary('libpgmplus-pic', src);

# end of file
